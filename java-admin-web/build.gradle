apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

group = 'net.compsoc.ox'
version = '0.0.1-SNAPSHOT'

description = """Compsoc Database - Web Admin Panel"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile project(':java-core')
    compile group: 'org.yaml', name: 'snakeyaml', version:'1.15'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version:'9.3.0.M1'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version:'9.3.0.M1'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlets', version:'9.3.0.M1'
    compile group: 'com.mitchellbosecke', name: 'pebble', version:'1.3.0'
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
        resources {
            srcDir 'resources'
        }
    }
}

task execute(type: JavaExec){
    main = 'net.compsoc.ox.web.admin.CompSocAdmin'
    classpath = sourceSets.main.runtimeClasspath
    if ( project.hasProperty("appArgs") ) {
        args Eval.me(appArgs)
    }
}

task fatJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Compsoc Admin Panel',
                   'Implementation-Version': version,
                   'Main-Class': 'net.compsoc.ox.web.admin.CompSocAdmin'
    }
    baseName = project.name + '-all'
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}